//--------------------------------------------------------------------
// deleteorder.mq4 
// Предназначен для использования в качестве примера в учебнике MQL4.
//--------------------------------------------------------------- 1 --
int start()                                     // Спец.функция start
  {
   string Symb=Symbol();                      // Финанс. инструмент
   double Dist=1000000.0;                     // Предустановка
   int Limit_Stop=-1;                         // Пока отложенных нет
   double Win_Price=WindowPriceOnDropped();     // Здесь брошен скрипт
//--------------------------------------------------------------- 2 --
   for(int i=1; i<=OrdersTotal(); i++)         // Цикл перебора ордер
     {
      if (OrderSelect(i-1,SELECT_BY_POS)==true) // Если есть следующий
        {                                      // Анализ ордеров:
         //------------------------------------------------------ 3 --
         if (OrderSymbol()!= Symb) continue;    // Не наш фин.инструм.
         int Tip=OrderType();                 // Тип ордера
         if (Tip<2) continue;                   // Рыночный ордер  
         //------------------------------------------------------ 4 --
         double Price=OrderOpenPrice();       // Цена ордера
         if (NormalizeDouble(MathAbs(Price-Win_Price),Digits)< //Выбор
            NormalizeDouble(Dist,Digits))       // самого близкого орд       
           {
            Dist=MathAbs(Price-Win_Price);      // Новое значение
            Limit_Stop=Tip;                   // Есть отложен. ордер
            int Ticket=OrderTicket();         // Номер ордера
           }                                   // Конец if
        }                                      //Конец анализа ордера
     }                                         // Конец перебора орд.
//--------------------------------------------------------------- 5 --
   switch(Limit_Stop)                          // По типу ордера
     {
      case 2: string Text= "BuyLimit ";         // Текст для BuyLimit
         break;                            // Выход из switch
      case 3: Text= "SellLimit ";               // Текст для SellLimit
         break;                            // Выход из switch
      case 4: Text= "BuyStopt ";                // Текст для BuyStopt
         break;                            // Выход из switch
      case 5: Text= "SellStop ";                // Текст для SellStop
         break;                            // Выход из switch
     }
//--------------------------------------------------------------- 6 --
   while(true)                                 // Цикл закрытия орд.
     {
      if (Limit_Stop==-1)                     // Если отложенных нет
        {
         Alert("По ",Symb," отложенных ордеров нет");
         break;                                 // Выход из цикла закр        
        }
      //--------------------------------------------------------- 7 --
      Alert("Попытка удалить ",Text," ",Ticket,". Ожидание ответа..");
      bool Ans=OrderDelete(Ticket);             // Удаление ордера
      //--------------------------------------------------------- 8 --
      if (Ans==true)                            // Получилось :)
        {
         Alert ("Удалён ордер ",Text," ",Ticket);
         break;                                 // Выход из цикла закр
        }
      //--------------------------------------------------------- 9 --
      int Error=GetLastError();               // Не получилось :(
      switch(Error)                            // Преодолимые ошибки
        {
         case  4: Alert("Торговый сервер занят. Пробуем ещё раз..");
            Sleep(3000);                  // Простое решение
            continue;                     // На след. итерацию
         case 137:Alert("Брокер занят. Пробуем ещё раз..");
            Sleep(3000);                  // Простое решение
            continue;                     // На след. итерацию
         case 146:Alert("Подсистема торговли занята. Пробуем ещё..");
            Sleep(500);                   // Простое решение
            continue;                     // На след. итерацию
        }
      switch(Error)                            // Критические ошибки
        {
         case 2 : Alert("Общая ошибка.");
            break;                        // Выход из switch
         case 64: Alert("Счет заблокирован.");
            break;                        // Выход из switch
         case 133:Alert("Торговля запрещена");
            break;                        // Выход из switch
         case 139:Alert("Ордер заблокирован и уже обрабатывается");
            break;                        // Выход из switch
         case 145:Alert("Модификация запрещена. ",
                              "Ордер слишком близок к рынку");
            break;                        // Выход из switch
         default: Alert("Возникла ошибка ",Error);//Другие варианты   
        }
      break;                                    // Выход из цикла закр
     }
//-------------------------------------------------------------- 10 --
   Alert ("Скрипт закончил работу -----------------------------");
   return;                                      // Выход из start()
  }
//-------------------------------------------------------------- 11 --