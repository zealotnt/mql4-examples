//--------------------------------------------------------------------
// Close_All.mqh
// Предназначен для использования в качестве примера в учебнике MQL4.
//--------------------------------------------------------------- 1 --
// Функция закрытия всех рыночных ордеров указанного типа
// Глобальные переменные:
// Mas_Ord_New   Массив ордеров последний известный
// Mas_Tip       Массив типов ордеров
//--------------------------------------------------------------- 2 --
int Close_All(int Tip)                    // Пользовательская функция
  {
   // int Tip                             // Тип ордера
   int Ticket=0;                          // Номер ордера
   double Lot=0;                          // Количество закр. лотов
   double Price_Cls;                      // Цена закрытия ордера
//--------------------------------------------------------------- 3 --
   while(Mas_Tip[Tip]>0)                 // До тех пор, пока есть ..
     {                                   //.. ордера заданного типа 
      for(int i=1; i<=Mas_Ord_New[0][0]; i++)// Цикл по живым ордерам
        {
         if(Mas_Ord_New[i][6]==Tip &&     // Среди ордеров нашего типа
            Mas_Ord_New[i][5]>Lot)        // .. выбираем самый дорогой
           {                              // Этот больше ранее найден.
            Lot=Mas_Ord_New[i][5];        // Наибольший найденный лот
            Ticket=Mas_Ord_New[i][4];     // Номер его ордера такой
           }
        }
      if (Tip==0) Price_Cls=Bid;          // Для ордеров Buy
      if (Tip==1) Price_Cls=Ask;          // Для ордеров Sell
      Inform(12,Ticket);                  // Сообщение о попытке закр.
      bool Ans=OrderClose(Ticket,Lot,Price_Cls,2);// Закрыть ордер !:)
      //--------------------------------------------------------- 4 --
      if (Ans==false)                     // Не получилось :( 
        {                                // Поинтересуемся ошибками:
         if(Errors(GetLastError())==false)// Если ошибка непреодолимая
            return;                       // .. то уходим.
        }
      //--------------------------------------------------------- 5 --
      Terminal();                         // Функция учёта ордеров 
      Events();                           // Отслеживание событий
     }
   return;                                // Выход из пользов. функции
  }
//--------------------------------------------------------------- 6 --
