//--------------------------------------------------------------------
// moveobjects.mq4
// Предназначен для использования в качестве примера в учебнике MQL4.
//--------------------------------------------------------------------
extern int   Len_Cn=50;              // Длина канала (баров)
extern color Col_Cn=Orange;          // Цвет канала 
//--------------------------------------------------------------- 1 --
int init()                             // Спец. функция init()
  {
   Create();                           // Вызов пользов. ф-ии создания
   return;                             // Выход из init()
  }
//--------------------------------------------------------------- 2 --
int start()                            // Спец. функция start()
  {
   datetime T2;                        // Вторая координата времени
   int Error;                          // Код ошибки
//--------------------------------------------------------------- 3 --   
   T2=ObjectGet("Obj_Reg_Ch",OBJPROP_TIME2);// Запрос коорд. t2
   Error=GetLastError();               // Получение кода ошибки
   if (Error==4202)                    // Если объекта нет :(
     {
      Alert("Канал регрессии находится под управлением",
            "\n Book_expert_82_2. Удалять нельзя.");
      Create();                        // Вызов пользов. ф-ии создания
      T2=Time[0];                      // Текущее значение коорд. t2
     }
//--------------------------------------------------------------- 4 --
   if (T2!=Time[0])                    // Если объект не на месте
     {
      ObjectMove("Obj_Reg_Ch", 0, Time[Len_Cn-1],0); //Новая коорд. t1
      ObjectMove("Obj_Reg_Ch", 1, Time[0],       0); //Новая коорд. t2
      WindowRedraw();                  // Перерисовка изображения 
     }
   return;                             // Выход из start()
  }
//--------------------------------------------------------------- 5 --
int deinit()                           // Спец. функция deinit()
  {
   ObjectDelete("Obj_Reg_Ch");         // Удаление объекта
   return;                             // Выход из deinit()
  }
//--------------------------------------------------------------- 6 --
int Create()                           // Пользовательская функция..
  {                                    // ..создания объекта
   datetime T1=Time[Len_Cn-1];         // Определение 1 коорд времени
   datetime T2=Time[0];                // Определение 2 коорд времени
   ObjectCreate("Obj_Reg_Ch",OBJ_REGRESSION,0,T1,0,T2,0);// Создание
   ObjectSet(   "Obj_Reg_Ch", OBJPROP_COLOR, Col_Cn);    // Цвет
   ObjectSet(   "Obj_Reg_Ch", OBJPROP_RAY,   false);     // Луч
   ObjectSet(   "Obj_Reg_Ch", OBJPROP_STYLE, STYLE_DASH);// Стиль
   ObjectSetText("Obj_Reg_Ch","Создан экспертом moveobjects",10);
   WindowRedraw();                     // Перерисовка изображения 
  }
//--------------------------------------------------------------- 7 --