//--------------------------------------------------------------------
// closeby.mq4 
// Предназначен для использования в качестве примера в учебнике MQL4.
//--------------------------------------------------------------- 1 --
int start()                                     // Спец.функция start
  {
   string Symb=Symbol();                      // Финанс. инструмент
   double Dist=1000000.0;                     // Предустановка
//--------------------------------------------------------------- 2 --
   while(true)                                 // Цикл обработки.. 
     {                                         // ..встречных ордеров
      double Hedg_Buy=-1.0;                   // Макс. стоимость Buy
      double Hedg_Sell= -1.0;                   // Макс.стоимость Sell
      for(int i=1; i<=OrdersTotal(); i++)      // Цикл перебора ордер
        {
         if(OrderSelect(i-1,SELECT_BY_POS)==true)// Если есть следующий
           {                                   // Анализ ордеров:
            //--------------------------------------------------- 3 --
            if (OrderSymbol()!= Symb) continue; // Не наш фин.инструм.
            int Tip=OrderType();              // Тип ордера
            if (Tip>1) continue;                // Отложенный ордер  
            //--------------------------------------------------- 4 --
            switch(Tip)                        // По типу ордера
              {
               case 0:                          // Ордер Buy
                  if (OrderLots()>Hedg_Buy)
                    {
                     Hedg_Buy=OrderLots();    // Выбираем макс.стоим
                     int Ticket_Buy=OrderTicket();//Номер ордера
                    }
                  break;                        // Из switch
               case 1:                          // Ордер Sell
                  if (OrderLots()>Hedg_Sell)
                    {
                     Hedg_Sell=OrderLots();   // Выбираем макс.стоим
                     int Ticket_Sell=OrderTicket();//Номер ордера
                    }
              }                                //Конец switch
           }                                   //Конец анализа ордера
        }                                      //Конец перебора орд.
      //--------------------------------------------------------- 5 --
      if (Hedg_Buy<0 || Hedg_Sell<0)            // Если нет ордера..
        {                                      // ..какого-то типа
         Alert("Все встречные ордера закрыты :)");// Сообщение  
         return;                                // Выход из start()
        }
      //--------------------------------------------------------- 6 --
      while(true)                              // Цикл закрытия 
        {
         //------------------------------------------------------ 7 --
         Alert("Попытка встречного закрытия. Ожидание ответа..");
         bool Ans=OrderCloseBy(Ticket_Buy,Ticket_Sell);// Закрытие 
         //------------------------------------------------------ 8 --
         if (Ans==true)                         // Получилось :)
           {
            Alert ("Выполнено встречное закрытие.");
            break;                              // Выход из цикла закр
           }
         //------------------------------------------------------ 9 --
         int Error=GetLastError();            // Не получилось :(
         switch(Error)                         // Преодолимые ошибки
           {
            case  4: Alert("Торговый сервер занят. Пробуем ещё раз..");
               Sleep(3000);               // Простое решение
               continue;                  // На след. итерацию
            case 137:Alert("Брокер занят. Пробуем ещё раз..");
               Sleep(3000);               // Простое решение
               continue;                  // На след. итерацию
            case 146:Alert("Подсистема торговли занята. Пробуем ещё..");
               Sleep(500);                // Простое решение
               continue;                  // На след. итерацию
           }
         switch(Error)                         // Критические ошибки
           {
            case 2 : Alert("Общая ошибка.");
               break;                     // Выход из switch
            case 64: Alert("Счет заблокирован.");
               break;                     // Выход из switch
            case 133:Alert("Торговля запрещена");
               break;                     // Выход из switch
            case 139:Alert("Ордер заблокирован и уже обрабатывается");
               break;                     // Выход из switch
            case 145:Alert("Модификация запрещена. ",
                                 "Ордер слишком близок к рынку");
               break;                     // Выход из switch
            default: Alert("Возникла ошибка ",Error);//Другие варианты   
           }
         Alert ("Скрипт закончил работу --------------------------");
         return;                                // Выход из start()
        }
     }                                         // Конец цикла обработ
//-------------------------------------------------------------- 10 --
  }                                            // Конец start()
//--------------------------------------------------------------------