//--------------------------------------------------------------------
// Errors.mqh
// Предназначен для использования в качестве примера в учебнике MQL4.
//--------------------------------------------------------------- 1 --
// Функция обработки ошибок.
// Возвращаемые значения:
// true  - если ошибка преодолимая (т.е. можно продолжать работу)
// false - если ошибка критическая (т.е. торговать нельзя)
//--------------------------------------------------------------- 2 --
bool Errors(int Error)                    // Пользовательская функция
  {
   // Error             // Номер ошибки   
   if(Error==0)
      return(false);                      // Нет ошибки
   Inform(15,Error);                      // Сообщение
//--------------------------------------------------------------- 3 --
   switch(Error)
     {   // Преодолимые ошибки:
      case 129:         // Неправильная цена
      case 135:         // Цена изменилась
         RefreshRates();                  // Обновим данные
         return(true);                    // Ошибка преодолимая
      case 136:         // Нет цен. Ждём новый тик.
         while(RefreshRates()==false)     // До нового тика
            Sleep(1);                     // Задержка в цикле
         return(true);                    // Ошибка преодолимая
      case 146:         // Подсистема торговли занята
         Sleep(500);                      // Простое решение
         RefreshRates();                  // Обновим данные
         return(true);                    // Ошибка преодолимая
         // Критические ошибки:
      case 2 :          // Общая ошибка
      case 5 :          // Старая версия клиентского терминала
      case 64:          // Счет заблокирован
      case 133:         // Торговля запрещена
      default:          // Другие варианты
         return(false);                   // Критическая ошибка
     }
//--------------------------------------------------------------- 4 --
  }
//--------------------------------------------------------------------