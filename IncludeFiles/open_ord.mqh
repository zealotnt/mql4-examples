//--------------------------------------------------------------------
// Open_Ord.mqh
// Предназначен для использования в качестве примера в учебнике MQL4.
//--------------------------------------------------------------- 1 --
// Функция открытия одного рыночного ордера указанного типа
// Глобальные переменные:
// int Mas_Tip                Массив типов ордеров
// int StopLoss               Значение StopLoss   (количество пунктов)
// int TakeProfit             Значение TakeProfit (количество пунктов)
//--------------------------------------------------------------- 2 --
int Open_Ord(int Tip)
  {
   int    Ticket,                        // Номер ордера
          MN;                            // MagicNumber
   double SL,                            // StopLoss (относит.знач.цены)
          TP;                            // TakeProf (относит.знач.цены)
//--------------------------------------------------------------- 3 --
   while(Mas_Tip[Tip]==0)                // До тех пор, пока  ..
     {                                   //.. не достигнут успех
      if (StopLoss<Level_new)            // Если меньше допустимого..
         StopLoss=Level_new;             // .. то допустимый
      if (TakeProfit<Level_new)          // Если меньше допустимого..
         TakeProfit=Level_new;           // ..то допустимый
      MN=TimeCurrent();                  // Простой MagicNumber
      Inform(13,Tip);                    // Сообщение о попытке откр
      if (Tip==0)                        // Будем открывать Buy
        {
         SL=Bid - StopLoss*  Point;      // StopLoss   (цена)
         TP=Bid + TakeProfit*Point;      // TakeProfit (цена)
         Ticket=OrderSend(Symbol(),0,Lots_New,Ask,2,SL,TP,"",MN);
        }
      if (Tip==1)                        // Будем открывать Sell
        {
         SL=Ask + StopLoss*  Point;      // StopLoss   (цена)
         TP=Ask - TakeProfit*Point;      // TakeProfit (цена)
         Ticket=OrderSend(Symbol(),1,Lots_New,Bid,2,SL,TP,"",MN);
        }
      //--------------------------------------------------------- 4 --
      if (Ticket<0)                       // Не получилось :( 
        {                                 // Поинтересуемся ошибками:
         if(Errors(GetLastError())==false)// Если ошибка непреодолимая
            return;                       // .. то уходим.
        }
      Terminal();                         // Функция учёта ордеров 
      Events();                           // Отслеживание событий
     }
//--------------------------------------------------------------- 5 --
   return;                                // Выход из пользов. функции
  }
//--------------------------------------------------------------- 6 --
