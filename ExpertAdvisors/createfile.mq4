//--------------------------------------------------------------------
// createfile.mq4
// Предназначен для использования в качестве примера в учебнике MQL4.
//--------------------------------------------------------------- 1 --
extern string Date_1="";   // 2007.05.11 10:30
extern string Text_1="";   // CHF Разрешения на строительство
extern string Date_2="";   // 2007.05.11 12:00
extern string Text_2="";   // GBP Ставка рефинансирования,2%,2.5%
extern string Date_3="";   // 2007.05.11 13:15
extern string Text_3="";   // EUR Встреча управляющих банков G10
extern string Date_4="";   // 2007.05.11 15:30
extern string Text_4="";   // USD Количество безработных в США
extern string Date_5="";   // 2007.05.11 18:30
extern string Text_5="";   // JPY Промышленное производство
//--------------------------------------------------------------- 2 --
int start()                            // Спец. функция start
  {
//--------------------------------------------------------------- 3 --
   int Handle,                         // Файловый описатель
   Qnt_Symb;                           // Количество записанных симв.
   string File_Name="News.csv";        // Имя файла
   string Erray[5,2];                  // Массив на 5 новостей
//--------------------------------------------------------------- 4 --
   Erray[0,0]=Date_1;                  // Заполняем массив значениями
   Erray[0,1]=Text_1;
   Erray[1,0]=Date_2;
   Erray[1,1]=Text_2;
   Erray[2,0]=Date_3;
   Erray[2,1]=Text_3;
   Erray[3,0]=Date_4;
   Erray[3,1]=Text_4;
   Erray[4,0]=Date_5;
   Erray[4,1]=Text_5;
//--------------------------------------------------------------- 5 --
   Handle=FileOpen(File_Name,FILE_CSV|FILE_WRITE,";");//Открытие файла
   if(Handle==-1)                      // Неудача при открытии файла
     {
      Alert("Ошибка при открытии файла. ",// Сообщение об ошибке 
              "Возможно, файл занят другим приложением");
      PlaySound("Bzrrr.wav");          // Звуковое сопровождение
      return;                          // Выход из start()      
     }
//--------------------------------------------------------------- 6 --
   for(int i=0; i<=4; i++)             // Цикл по всему массиву
     {
      if(StringLen(Erray[i,0])== 0  || // Если значение первой или ..
         StringLen(Erray[i,1])== 0)    // ..второй перемен. не введено
         break;                        // .. то выход из цикла
      Qnt_Symb=FileWrite(Handle,Erray[i,0],Erray[i,1]);//Запись в файл
      if(Qnt_Symb < 0)                 // Если не получилось
        {
         Alert("Ошибка записи в файл ",GetLastError());// Сообщение
         PlaySound("Bzrrr.wav");       // Звуковое сопровождение
         FileClose( Handle );          // Закрываем файл
         return;                       // Выход из start()      
        }
     }
//--------------------------------------------------------------- 7 --
   FileClose( Handle );                // Закрываем файл
   Alert("Файл ",File_Name," создан.");// Сообщение
   PlaySound("Bulk.wav");              // Звуковое сопровождение
   return;                             // Выход из start()
  }
//--------------------------------------------------------------- 8 --