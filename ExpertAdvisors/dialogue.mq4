//--------------------------------------------------------------------
// dialogue.mq4
// Предназначен для использования в качестве примера в учебнике MQL4.
//--------------------------------------------------------------- 1 --
#include <WinUser32.mqh>               // Нужно для MessageBox
extern double Time_News=15.30;         // Время важных новостей
bool Question=false;                   // Флажок (вопрос ещё не задан)
//--------------------------------------------------------------- 2 --
int start()                            // Спец. функция start
  {
   PlaySound("tick.wav");              // На каждом тике
   double Time_cur=Hour()+ Minute()/100.0;// Текущее время (double)
   if (OrdersTotal()>0 && Question==false && Time_cur>=Time_News-0.05)
     {                                 // При некоторых условиях
      PlaySound("news.wav");           // На каждом тике
      Question=true;                   // Флажок (вопрос уже задан)
      int ret=MessageBox("Время важных новостей. Закрыть все ордера?",
      "Вопрос", MB_YESNO|MB_ICONQUESTION|MB_TOPMOST); // Диалог. окно
      //--------------------------------------------------------- 3 --
      if(ret==IDYES)                   // Если получен ответ ДА
         Close_Orders();               // То закрываем все ордера
     }
   return;                             // Выход 
  }
//--------------------------------------------------------------- 4 --
void Close_Orders()                    // Польз. ф-ия закрытия ордеров
  {
   Alert("Работает функция закрытия всех ордеров.");// Для иллюстрации
   return;                             // Выход 
  }
//--------------------------------------------------------------- 5 --